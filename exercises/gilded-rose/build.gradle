import org.flywaydb.gradle.task.FlywayMigrateTask

import static nu.studer.gradle.jooq.JooqEdition.OSS

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'nu.studer.jooq' version '9.0'
    id "org.flywaydb.flyway" version "9.16.3"
}

repositories {
    mavenCentral()
}

var dbDir = "${projectDir.absolutePath}/databases"
ext.jacksonVersion = '2.14.1'
ext.testJdbcUrl = "jdbc:h2:$dbDir/gilded-rose-test-db"
ext.devJdbcUrl = providers.environmentVariable('JDBC_URL').orElse("jdbc:h2:$dbDir/gilded-rose.db").get()
ext.databaseUsername = providers.environmentVariable('DB_USERNAME').orElse('gilded').get()
ext.databasePassword = providers.environmentVariable('DB_PASSWORD').orElse('rose').get()


dependencies {
    implementation libs.kotlinx.coroutines.core
    implementation libs.bundles.http4k
    implementation libs.result4k
    implementation libs.bundles.http4k

    implementation "org.slf4j:slf4j-jdk14:1.7.36"

    implementation libs.bundles.jackson

    implementation 'com.h2database:h2:2.2.224'
    implementation "com.zaxxer:HikariCP:5.0.1"

    implementation "org.jooq:jooq:3.19.7"

    testImplementation libs.bundles.junit

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "io.strikt:strikt-core:0.34.1"

    testImplementation 'com.microsoft.playwright:playwright:1.36.0'

    testImplementation libs.bundles.http4k.testing

    jooqGenerator 'com.h2database:h2:2.2.224'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    compilerOptions.freeCompilerArgs.add("-Xcontext-receivers")
}

compileTestKotlin {
    compilerOptions.freeCompilerArgs.add("-Xcontext-receivers")
}

flyway {
    url = "$testJdbcUrl"
    user = "$databaseUsername"
    password = "$databasePassword"
}

tasks.register('flywayMigrateDev', FlywayMigrateTask) {
    url = "$devJdbcUrl"
    user = "$databaseUsername"
    password = "$databasePassword"
}

jooq {
    version = '3.19.7'  // default (can be omitted)
    edition = OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = "$testJdbcUrl"
                    user = "$databaseUsername"
                    password = "$databasePassword"
                    properties {
                        property {
                            key = 'ssl'
                            value = 'false'
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        inputSchema = 'PUBLIC'
                        forcedTypes {
                            forcedType {
                                name = 'instant'
                                includeExpression = '.*'
                                includeTypes = 'TIMESTAMP\\ WITH\\ TIME\\ ZONE'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.gildedrose.db'
                        directory = 'build/generated-src/jooq/main'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

tasks.register('initDbs') {
    group "*** gilded rose exercise ***"
    dependsOn 'flywayMigrate', 'flywayMigrateDev'
}

tasks.named('compileKotlin') {
    dependsOn 'generateJooq'
}
